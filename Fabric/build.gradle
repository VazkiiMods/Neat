plugins {
    id 'fabric-loom' version '1.3.8'
}

version = "${mc_version}-${build_number}-FABRIC"

loom {
    accessWidenerPath = file("src/main/resources/neat.accesswidener")
    mixin.defaultRefmapName = "neat.refmap.json"

    runs {
        configureEach {
            runDir "../run"
            ideConfigGenerated(true)
        }
    }

}

repositories {
    maven {
        name = "Modmuss"
        url = "https://maven.modmuss50.me/"
        content {
            includeGroup "me.zeroeightsix"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${mc_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:0.15.6"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.92.1+1.20.1"

    compileOnly project(":Xplat")

    modImplementation "me.zeroeightsix:fiber:0.23.0-2"
    include "me.zeroeightsix:fiber:0.23.0-2"
}

compileJava {
    source(project(":Xplat").sourceSets.main.allSource)
    options.compilerArgs << "-Xlint:all,-classfile,-processing,-deprecation,-serial" << "-Werror"

}

sourcesJar {
    from project(":Xplat").sourceSets.main.allJava
}

processResources {
    from project(":Xplat").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
