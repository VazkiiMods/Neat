buildscript {
    repositories {
        maven {
            url = 'https://maven.minecraftforge.net'
        }
        maven {
            url = 'https://repo.spongepowered.org/repository/maven-public/'
            content { includeGroup "org.spongepowered" }
        }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:6.0.6'
        classpath 'org.spongepowered:mixingradle:0.7.33'
    }
}

plugins {
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'org.spongepowered.mixin' version '0.7-20230803.124500-3'
    id 'eclipse'
    id 'idea'
}

version = "${project.mc_version}-${project.build_number}-FORGE"

mixin {
    add sourceSets.main, "neat.refmap.json"
    config "neat.mixins.json"
}

minecraft {
    mappings channel: "official", version: "${project.mc_version}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('../run')
            property 'forge.logging.console.level', 'debug'
            ideaModule "${rootProject.name}.${project.name}.main"

            mods {
                neat {
                    source sourceSets.main
                    source project(":Xplat").sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        name = 'Sponge Snapshots'
        url = 'https://repo.spongepowered.org/repository/maven-snapshots/'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.mc_version}-${project.forge_version}"
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
    compileOnly project(":Xplat")
    annotationProcessor 'org.spongepowered:mixin:0.8.6-20240121.184834-1:processor'
}

compileJava {
    source(project(":Xplat").sourceSets.main.allSource)
    options.compilerArgs << "-Xlint:all,-classfile,-processing,-deprecation,-serial" << "-Werror"
}

sourcesJar {
    from project(":Xplat").sourceSets.main.allJava
}

processResources {
    from project(":Xplat").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"   : "${project.mod_id}",
                "Specification-Vendor"  : "vazkii",
                "Specification-Version" : "1", // We are version 1 of ourselves
                "Implementation-Title"  : "${project.mod_id}",
                "Implementation-Version": "${project.version}",
                "Implementation-Vendor" : "vazkii",
        ])
    }
}

defaultTasks 'clean', 'build'
